apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {

    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "pl.krystiankaniowski.weatherapp"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }

    buildTypes {

        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }

        debug {

            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def GOOGLE_PLACES_API = properties.getProperty('api.googleplacesapi')
            def OPEN_WEATHER_MAP_API = properties.getProperty('api.openweathermap')

            buildConfigField "String", "GOOGLE_PLACES_API", "\"$GOOGLE_PLACES_API\""
            buildConfigField "String", "OPEN_WEATHER_MAP_API", "\"$OPEN_WEATHER_MAP_API\""

        }

    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }


}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Test
    testCompile "junit:junit:$junitVersion"

    // Support
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"

    // Butterknife
    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    // RxJava
    compile "io.reactivex:rxandroid:$rxandroidVersion"
    compile "io.reactivex:rxjava:$rxjavaVersion"

    // EventBus
    compile "org.greenrobot:eventbus:$eventBusVersion"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

}

repositories {
    mavenCentral()
}
